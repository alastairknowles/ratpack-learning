import com.mysql.cj.jdbc.MysqlDataSource
import com.querydsl.sql.MySQLTemplates
import com.querydsl.sql.codegen.MetaDataExporter

import java.sql.Connection


buildscript {
    dependencies {
        classpath group: 'mysql', name: 'mysql-connector-java', version: '8.0.16'
        classpath group: 'com.querydsl', name: 'querydsl-sql-codegen', version: '4.2.1'
        classpath group: 'com.querydsl', name: 'querydsl-sql', version: '4.2.1'
    }
}

plugins {
    id 'org.flywaydb.flyway' version '5.2.4'
}

apply plugin: 'java'
apply plugin: 'groovy'

group = 'real.al.knowles'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8


repositories {
    mavenCentral()
}

def databaseHost = 'localhost'
def databasePort = 3306
def databaseName = 'testman'
def databaseUser = 'testman'
def databasePassword = 'testman'

dependencies {
    compile group: 'io.ratpack', name: 'ratpack-core', version: '1.6.1'
    compile group: 'io.ratpack', name: 'ratpack-guice', version: '1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.16'
    compile group: 'com.querydsl', name: 'querydsl-sql', version: '4.2.1'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

    testCompile group: 'io.ratpack', name: 'ratpack-test', version: '1.6.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'org.spockframework', name: 'spock-guice', version: '1.3-groovy-2.5'

}

test {
    dependsOn cleanTest
    testLogging {
        events = [
                'passed',
                'skipped',
                'failed',
                'standardOut',
                'standardError']
    }
}

flyway {
    url = 'jdbc:mysql://' + databaseHost + ':' + databasePort + '/' + databaseName
    user = databaseUser
    password = databasePassword
    schemas = [databaseName]
}

sourceSets { generated }
sourceSets.generated.java.srcDirs = ['src/main/generated']

task queryDsl() {
    dependsOn flywayMigrate
    outputs.upToDateWhen { false }
    doLast {
        MetaDataExporter exporter = new MetaDataExporter()
        Class.forName('com.mysql.cj.jdbc.Driver').newInstance()

        MysqlDataSource dataSource = new MysqlDataSource()
        dataSource.setServerName(databaseHost)
        dataSource.setPortNumber(databasePort)
        dataSource.setDatabaseName(databaseName)
        dataSource.setUser(databaseUser)
        dataSource.setPassword(databasePassword)

        dataSource.connection.with {
            Connection connection ->
                def configuration = new com.querydsl.sql.Configuration(MySQLTemplates.DEFAULT)
                exporter.schemaPattern = 'testman'
                exporter.packageName = 'real.al.knowles.ratpack.learning.schema'
                exporter.targetFolder = new File('src/main/java')
                exporter.configuration = configuration
                exporter.export(connection.getMetaData())
        }
    }
}

assemble.dependsOn(queryDsl)
