import com.querydsl.sql.MySQLTemplates
import com.querydsl.sql.codegen.MetaDataExporter

import java.sql.Connection

import org.mariadb.jdbc.MariaDbDataSource


buildscript {
    dependencies {
        classpath group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.3'
        classpath group: 'com.querydsl', name: 'querydsl-sql-codegen', version: '4.2.1'
        classpath group: 'com.querydsl', name: 'querydsl-sql', version: '4.2.1'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'org.flywaydb.flyway' version '5.2.4'
}

group = 'real.al.knowles'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def databaseProperties = new Properties()
file("src/main/resources/db/database.properties").withInputStream {
    inputStream -> databaseProperties.load(inputStream)
}

def databaseDriver = databaseProperties.getProperty('database.driver')
def databaseUrl = databaseProperties.getProperty('database.url')
def databaseName = databaseProperties.getProperty('database.name')
def databaseUser = databaseProperties.getProperty('database.user')
def databasePassword = databaseProperties.getProperty('database.password')

dependencies {
    compile group: 'io.ratpack', name: 'ratpack-core', version: '1.6.1'
    compile group: 'io.ratpack', name: 'ratpack-guice', version: '1.6.1'
    compile group: 'io.ratpack', name: 'ratpack-jdbc-tx', version: '1.6.1'
    compile group: 'io.ratpack', name: 'ratpack-dropwizard-metrics', version: '1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.3'
    compile group: 'com.querydsl', name: 'querydsl-sql', version: '4.2.1'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

    testCompile group: 'io.ratpack', name: 'ratpack-test', version: '1.6.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'org.spockframework', name: 'spock-guice', version: '1.3-groovy-2.5'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
}

test {
    dependsOn cleanTest
    testLogging {
        events = [
                'passed',
                'skipped',
                'failed',
                'standardOut',
                'standardError']
    }
}

flyway {
    url = databaseUrl
    user = databaseUser
    password = databasePassword
    schemas = [databaseName]
}

sourceSets { generated }
sourceSets.generated.java.srcDirs = ['src/main/generated']

task queryDsl() {
    dependsOn flywayMigrate
    outputs.upToDateWhen { false }
    doLast {
        MetaDataExporter exporter = new MetaDataExporter()
        Class.forName(databaseDriver).newInstance()

        MariaDbDataSource dataSource = new MariaDbDataSource(databaseUrl)
        dataSource.connection.with {
            Connection connection ->
                //noinspection GroovyAssignabilityCheck
                def configuration = new com.querydsl.sql.Configuration(MySQLTemplates.DEFAULT)
                exporter.schemaPattern = databaseName
                exporter.packageName = 'real.al.knowles.ratpack.learning.schema'
                exporter.targetFolder = new File('src/main/java')
                exporter.configuration = configuration
                exporter.export(connection.getMetaData())
        }
    }
}

assemble.dependsOn(queryDsl)
